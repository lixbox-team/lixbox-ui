project(':lixbox-ui-faces-preview'){
    apply plugin: 'war'
    
	dependencies {             
	    //project
        compile project(':lixbox-ui-faces')
        
        //divers
        compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '4.0.3'
        compileOnly group: 'com.sun.faces', name: 'jsf-impl', version: '2.2.19'
        compileOnly group: 'jakarta.faces', name: 'jakarta.faces-api', version: '2.3.1'
        
        compileOnly group: 'jakarta.enterprise', name: 'jakarta.enterprise.cdi-api', version: '2.0.2'
        compileOnly group: 'jakarta.inject', name: 'jakarta.inject-api', version: '1.0'
        
        testCompile 'junit:junit:4.12'
	}
    
    jar{
       manifest {
            attributes(
                "Implementation-Title": project.name, 
                "Implementation-Version": archiveVersion, 
                "Implementation-Timestamp": new Date()
            )
        }
    }
    
    eclipse{
        project{
            natures 'org.jboss.tools.ws.jaxrs.nature'
        }
        wtp{
            component {
                resource sourcePath: 'WebContent', deployPath: '/'
                file{ 
                    whenMerged{
                         wtpComponent -> wtpComponent.properties.clear()
                    }
                }
            }
            facet {
                facet name: 'jst.cdi', version: '2.0'
                facet name: "jst.web", version: "4.0"
                facet name: "jst.jaxrs", version: "2.1"
                file{
                    whenMerged {                        
                        provider -> 
                            def installedJstFacet = provider.facets.find { facet -> facet.name == 'jst.utility' && facet.type == org.gradle.plugins.ide.eclipse.model.Facet.FacetType.installed}
                            provider.facets.remove(installedJstFacet)
                            installedJstFacet = provider.facets.find { facet -> facet.name == 'jst.web' && facet.version == '2.4' && facet.type == org.gradle.plugins.ide.eclipse.model.Facet.FacetType.installed}
                            provider.facets.remove(installedJstFacet)
                    }
                }
            }   
        }
    }
	
	sonarqube {
		properties {
           property "sonar.projectKey", "${config.desc_project.artefact.group}.${rootProject.name}:${project.name}"
           property "sonar.projectName", "${project.name}"
		   property "sonar.sources","src/main"
		   property "sonar.tests","src/test, src/integTest"
        }
	}
    
    smokeTest {
        systemProperty 'service.host', "${config.server.protocol}://${config.server.hostname}:${config.server.port}"
        systemProperty 'service.uri', "${config.desc_project.uri.ui}"
    }
    
    war {
        from 'WebContent'
        classpath fileTree('WebContent/WEB-INF/lib')
    }
}